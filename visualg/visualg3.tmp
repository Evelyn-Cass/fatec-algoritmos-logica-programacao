Algoritmo "JOGO_VELHA"
Var
   jogo: vetor[1..3,1..3] de caracter
   linha, coluna: inteiro
   jogador: caractere
   i, j, jogada, resto:inteiro
   fim, verificacao:logico

Procedimento mostrarJogo
Inicio
   Escreval("Jogo da Velha!")
   Escreval

   para i de 1 ate 3 faca
      para j de 1 ate 3 faca
         se ( j <> 3) entao
            Escreva(" ",jogo[i,j],"  |")
         senao
            Escreva(" ",jogo[i,j],"  ")
         fimSe
      fimPara
      se ( i <> 3) entao
         Escreval
         Escreval("--------------")
      fimSe
   fimPara
   Escreval
   Escreval
FimProcedimento

Inicio

   Para i de 1 ate 3 faca
      Para j de 1 ate 3 faca
         jogo[i,j] <- " "
      fimPara
   fimPara
   fim <- falso
   Para jogada de 1 até 9 faca

      resto <- jogada mod 2
      Se (resto = 0) entao
         jogador <- "X"
      Senao
         jogador <- "O"
      fimSe
      mostrarJogo
      Escreval("JOGADOR ",jogador)
      Escreval("Qual posição você deseja?")

      verificacao <- falso
      Repita
         Repita
            Escreval("Informe o número da linha:")
            leia(linha)

            Se (linha <= 0) ou (linha >= 4) entao

               LimpaTela
               mostrarJogo
               Escreval("Linha inválida!")
               Escreval("A linha deve ser 1, 2 ou 3")
               Escreval("JOGADOR ",jogador)

            fimSe

         até (linha > 0) e (linha < 4)

         LimpaTela

         mostrarJogo
         Escreval("JOGADOR ",jogador)
         Escreval("Qual posição você deseja?")

         Repita
            Escreval("Informe o número da coluna:")
            leia(coluna)

            se (coluna <= 0) ou (coluna >= 4) então
               LimpaTela
               mostrarJogo
               Escreval("Coluna inválida!")
               Escreval("A coluna deve ser 1, 2 ou 3")
               Escreval("JOGADOR ",jogador)
            fimSe


         ate (coluna > 0) e (coluna < 4)

         se (jogo[linha,coluna] <> " ") então
            LimpaTela

            mostrarJogo
            Escreval("A Posição [",linha,",",coluna,"] está ocupada!")
            Escreval("JOGADOR ",jogador)
            Escreval("Informe uma posição desocupada.")


         senao
            verificacao <- verdadeiro
            jogo[linha,coluna] <- jogador
         fimse

      ate (verificacao =  verdadeiro)
      LimpaTela

      Para i de 1 até 3 faça
         Se (jogo[i,1] <> " ") e (jogo[i,1] = jogo[i,2]) e (jogo[i,2] = jogo[i,3]) então
            fim <- verdadeiro
         fimse
         Se (jogo[1,i] <> " ") e (jogo[1,i] = jogo[2,i]) e (jogo[2,i] = jogo[3,i]) então
            fim <- verdadeiro
         fimse
      fimPara
      Se (jogo[1,1] <> " ") e (jogo[1,1] = jogo[2,2]) e (jogo[2,2] = jogo[3,3]) então
         fim <- verdadeiro
      fimse
      Se (jogo[1,3] <> " ") e (jogo[1,3] = jogo[2,2]) e (jogo[2,2] = jogo[3,1]) então
         fim <- verdadeiro
      fimse

      se fim = verdadeiro entao
         mostrarJogo
         Escreval ("Jogador '",jogador ,"' GANHOU!")
         FimAlgoritmo
      fimSe

   fimpara
   mostrarJogo
   Escreval("DEU VELHA!")
Fimalgoritmo